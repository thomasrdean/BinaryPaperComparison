# Copyright (c) 2021 by the Zeek Project. See LICENSE for details.

module DNS;

import spicy;

public type PDU = unit {
    transactionId    : uint16;
    flags : bytes &size=2;

    numQuestion: uint16;
    numAnswer: uint16;
    numAuthority: uint16;
    numAdditional: uint16;

    question:   Query[self.numQuestion];
    answer:     ResourceRecord[self.numAnswer];
    authority:  ResourceRecord[self.numAuthority];
    additional: ResourceRecord[self.numAdditional];
    
    on %done { print self; }
};

type Query = unit {
    name:  Domain;
    type_:  uint16;
    class: uint16;
};

type ResourceRecord = unit() {
    name:   Domain;
    type_:  uint16 &convert=RRType($$);
    class:  uint16;
    ttl:    uint32;
    dataLength:  uint16;

    switch ( self.type_ ) {
        RRType::A    -> rr_body_a:       addr &ipv4;
        RRType::NS   -> rr_body_nameServer:   Domain;
        RRType::CNAME-> rr_body_cname: Domain;
        RRType::SOA  -> rr_body_soa:     RDataSOA;
        RRType::PTR  -> rr_body_domainName: Domain;
        RRType::MX   -> rr_body_mx:      RDataMX;
        RRType::TXT  -> rr_body_txt:     CharacterString;
        RRType::AAAA -> rr_body_a:       addr &ipv6;
        # OPT
        # DS
        # KEY
        # RRSIG
        # NSEC3
        RRType::SRV  -> rr_body_srv:     RDataSRV;
        *            -> rdata:   bytes &size=self.dataLength;
    };
};

type RRType = enum {
    A = 1,
    NS = 2,
    MD = 3,
    MF = 4,
    CNAME = 5,
    SOA = 6,
    MB = 7,
    MG = 8,
    MR = 9,
    NULL = 10,
    WKS = 11,
    PTR = 12,
    HINFO = 13,
    MINFO = 14,
    MX = 15,
    TXT = 16,
    AAAA = 28,
    NBS = 32,
    SRV= 33,
    A6 = 38,
    EDNS = 41
};

type RDataMX = unit {
    preference: uint16;
    name:        Domain;
};

type RDataSOA = unit {
    mname:      Domain;
    rname:      Domain;
    serial:     uint32;
    refresh:    uint32 &convert=cast<interval>($$);
    retry:      uint32 &convert=cast<interval>($$);
    expire:     uint32 &convert=cast<interval>($$);
    minimum:    uint32 &convert=cast<interval>($$);
};

type RDataSRV = unit {
    priority_:  uint16;
    weight:     uint16;
    port_:      uint16;
    target:     Domain;
};

type CharacterString = unit {
    len: uint8;
    data: bytes &size=(self.len);
};

type Domain = unit {
    words: Label[] &until=($$.len.offset == 0 || $$.len.compressed != 0);
};

type Pointer = unit() {
    len: bitfield(16) {
        offset: 0..13;
        };
    };

type Label = unit() {
    len: bitfield(8) {
        offset: 0..5;
        compressed: 6..7;
    };

    switch ( self.len.compressed ) {
        0 ->    label: bytes &size=self.len.offset;
        3 ->    ptr: uint8;
    };
};
